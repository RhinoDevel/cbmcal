#RetroDevStudio.MetaData.BASIC:2049,BASIC V2,uppercase,10,10
2000 REM --------------------------
2010 REM --- OTHER GLOBAL VARS. ---
2020 REM --------------------------
2030 DN=8:REM DISK DRIVE NUMBER
2040 DIM DW(11):REM FOR WEEKDAY-BY-DATE
2050 DIM WD$(6):REM THE WEEKDAYS
2060 DIM MD(11):REM NON-LEAP MONTH DAYS
2070 I=0:J=0:REM COUNTERS
2080 B0=0:B1=0:B2=0:REM NUMBER BUFFERS
2090 B0$="":REM STRING BUFFER
2100 IN$="":REM FOR USER INPUT
2110 CO$="":REM STORES A USER COMMAND
2120 YE$="":MO$="":DA$="":REM A DATE.
2130 YE=2024:MO=3:DA=0:REM DATE AS INTS.
2140 WD=0:REM INDEX INTO WD$.
2150 REM ------------------------
2160 REM --- INIT "CONSTANTS" ---
2170 REM ------------------------
2180 FOR I=0 TO 11:READ DW(I):NEXT I
2190 FOR I=0 TO 6:READ WD$(I):NEXT I
2200 FOR I=0 TO 11:READ MD(I):NEXT I
2210 REM ------------------------------
2220 REM --- OPEN DISK CMD. CHANNEL ---
2230 REM ------------------------------
2240 OPEN15,DN,15
2250 REM --------------------
2260 REM --- JUMP TO MAIN ---
2270 REM --------------------
2280 GOTO 3080
2290 REM -------------------
2300 REM --- SUBROUTINES ---
2310 REM -------------------
2320 REM ******************************
2330 REM *** LEAP YEAR YES/NO TO B2 ***
2340 REM ******************************
2350 IF YE/100<>INT(YE/100) GOTO 2380
2360 IF YE/400=INT(YE/400) GOTO 2390
2370 B2=0:RETURN:REM NOT A LEAP YEAR
2380 IF YE/4<>INT(YE/4) GOTO 2370
2390 B2=1:RETURN:REM IT IS A LEAP YEAR
2400 REM *******************
2410 REM *** PRINT MONTH ***
2420 REM *******************
2430 REM TITLE ROW (THE WEEKDAYS):
2440 PRINT " ";
2450 FOR I=0 TO 5
2460 PRINT WD$(I);"  ";
2470 NEXT I
2480 PRINT WD$(6)
2490 REM 1ST (MAYBE INCOMPL.) DAY ROW:
2500 PRINT " ";
2510 FOR I=0 TO 5
2520 IF I>=WD THEN 2540
2530 PRINT "   ";:GOTO 2550
2540 PRINT I-WD+1;" ";
2550 NEXT I
2560 PRINT I-WD+1;" "
2570 REM FOLLOWING (COMPLETE) DAY ROWS:
2580 B0=I-WD+1+1
2590 B1=MD(MO-1)
2600 IF MO<>2 THEN 2640:REM NO FEBRUARY
2610 GOSUB2350:REM IS A LEAP YEAR?
2620 IF B2=0 THEN 2640:REM NO LEAP YEAR
2630 B1=B1+1:REM 29 DAYS IN FEBRUARY
2640 J=0
2650 FOR I=B0 TO B1
2660 IF I<10 THEN PRINT " ";
2670 PRINT I;
2680 J=J+1
2690 IF J<7 THEN 2720
2700 PRINT""
2710 J=0
2720 NEXT I
2730 PRINT""
2740 RETURN
2750 REM *****************************
2760 REM *** FILL WD BY YE,MO & DA ***
2770 REM *****************************
2780 B0=YE
2790 IF MO<3 THEN B0=B0-1
2800 WD=B0+INT(B0/4)
2810 WD=WD-INT(B0/100)
2820 WD=WD+INT(B0/400)
2830 WD=WD+DW(MO-1)+DA
2840 WD=WD-INT(WD/7)*7
2850 WD=WD+6:WD=WD-INT(WD/7)*7
2860 RETURN
2870 REM *******************************
2880 REM *** READ DISK ERROR CHANNEL ***
2890 REM *******************************
2900 INPUT#15,B0,B0$,B1,B2
2910 RETURN
2920 REM *****************************
2930 REM *** MONTH FROM INPUT CMD. ***
2940 REM *****************************
2950 YE$=MID$(IN$,2,4)
2960 MO$=MID$(IN$,6,2)
2970 REM *** MONTH FROM YE$ AND MO$ ***
2980 YE=VAL(YE$)
2990 MO=VAL(MO$)
3000 REM *** MONTH FROM YE AND MO ***
3010 DA=1
3020 GOSUB 2780:REM FILLS WD
3030 GOSUB 2440:REM PRINTS MONTH
3040 RETURN
3050 REM --------------------
3060 REM --- MAIN ROUTINE ---
3070 REM --------------------
3080 PRINT"":GOSUB 3010
3090 INPUT"";IN$:REM READS COMMANDLINE
3100 CO$=LEFT$(IN$,1):REM GETS COMMAND
3110 IF CO$<>"M" THEN 3130
3120 GOSUB 2950:GOTO 3090
3130 IF CO$<>"Q" THEN 3150
3140 GOTO3190:REM GOES TO EXIT CODE
3150 GOTO 3090
3160 REM >>>>>>>>><<<<<<<<
3170 REM >>> EXIT CODE <<<
3180 REM >>>>>>>>><<<<<<<<
3190 CLOSE15:END
3200 REM ------------
3210 REM --- DATA ---
3220 REM ------------
3230 REM FOR WEEKDAY-BY-DATE CALC.:
3240 DATA 0,3,2,5,0,3,5,1,4,6,2,4
3250 REM FOR WEEKDAY ITERATION:
3260 DATA MO,TU,WE,TH,FR,SA,SU
3270 REM DAYS OF MONTH (NON-LEAP YEAR)
3280 DATA 31,28,31,30,31,30,31,31,30,31,30,31