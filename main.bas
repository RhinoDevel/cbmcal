#RetroDevStudio.MetaData.BASIC:2049,BASIC V2,uppercase,10,10
1000 REM --- OTHER GLOBAL VARS. --
1010 DIM DW(11):REM FOR WEEKDAY-BY-DATE
1020 DIM WD$(6):REM THE WEEKDAYS
1030 DIM MD(11):REM NON-LEAP MONTH DAYS
1040 I=0:J=0:REM COUNTERS
1050 B0=0:B1=0:B2=0:REM NUMBER BUFFERS
1060 IN$="":REM FOR USER INPUT
1070 CO$="":REM STORES A USER COMMAND
1080 YE$="":MO$="":DA$="":REM A DATE.
1090 YE=0:MO=0:DA=0:REM DATE AS INTS.
1100 WD=0:REM INDEX INTO WD$.
1110 REM --- INIT "CONSTANTS" ---
1120 FOR I=0 TO 11:READ DW(I):NEXT I
1130 FOR I=0 TO 6:READ WD$(I):NEXT I
1140 FOR I=0 TO 11:READ MD(I):NEXT I
1150 REM --- JUMP TO MAIN ---
1160 GOTO 1780
1170 REM --- SUBROUTINES ---
1180 REM *** LEAP YEAR YES/NO TO B2 ***
1190 IF YE/100<>INT(YE/100) GOTO 1220
1200 IF YE/400=INT(YE/400) GOTO 1230
1210 B2=0:RETURN:REM NOT A LEAP YEAR
1220 IF YE/4<>INT(YE/4) GOTO 1210
1230 B2=1:RETURN:REM IT IS A LEAP YEAR
1240 REM *** PRINT MONTH ***
1250 PRINT " ";
1260 FOR I=0 TO 5
1270 PRINT WD$(I);"  ";
1280 NEXT I
1290 PRINT WD$(6)
1300 PRINT " ";
1310 FOR I=0 TO 5
1320 IF I>=WD THEN 1340
1330 PRINT "  î¹  ";:GOTO 1350
1340 PRINT I-WD+1;" ";
1350 NEXT I
1360 PRINT I-WD+1;" "
1370 B0=I-WD+1+1
1380 B1=MD(MO-1)
1390 IF MO<>2 THEN 1430:REM NO FEBRUARY
1400 GOSUB1190:REM IS A LEAP YEAR?
1410 IF B2=0 THEN 1430:REM NO LEAP YEAR
1420 B1=B1+1:REM 29 DAYS IN FEBRUARY
1430 J=0
1440 FOR I=B0 TO B1
1450 IF I<10 THEN PRINT " ";
1460 PRINT I;
1470 J=J+1
1480 IF J<7 THEN 1510
1490 PRINT""
1500 J=0
1510 NEXT I
1520 PRINT""
1530 RETURN
1540 REM *** FILL WD BY YE,MO & DA ***
1550 B0=YE
1560 IF MO<3 THEN B0=B0-1
1570 WD=B0+INT(B0/4)
1580 WD=WD-INT(B0/100)
1590 WD=WD+INT(B0/400)
1600 WD=WD+DW(MO-1)+DA
1610 WD=WD-INT(WD/7)*7
1620 WD=WD+6:WD=WD-INT(WD/7)*7
1630 RETURN
1640 REM *** MONTH FROM INPUT CMD. ***
1650 YE$=MID$(IN$,2,4)
1660 MO$=MID$(IN$,6,2)
1670 REM *** MONTH FROM YE$ AND MO$ ***
1680 YE=VAL(YE$)
1690 MO=VAL(MO$)
1700 REM *** MONTH FROM YE AND MO ***
1710 DA=1
1720 GOSUB 1550:REM FILLS WD
1730 GOSUB 1250:REM PRINTS MONTH
1740 RETURN
1750 REM --------------------
1760 REM --- MAIN ROUTINE ---
1770 REM --------------------
1780 INPUT"";IN$:REM READS COMMANDLINE
1790 CO$=LEFT$(IN$,1):REM GETS COMMAND
1800 IF CO$<>"M" THEN 1820
1810 GOSUB 1650:GOTO 1780
1820 IF CO$<>"Q" THEN 1840
1830 END
1840 GOTO 1780
1850 END
1860 REM --- DATA ---
1870 REM FOR WEEKDAY-BY-DATE CALC.:
1880 DATA 0,3,2,5,0,3,5,1,4,6,2,4
1890 REM FOR WEEKDAY ITERATION:
1900 DATA MO,TU,WE,TH,FR,SA,SU
1910 REM DAYS OF MONTH (NON-LEAP YEAR)
1920 DATA 31,28,31,30,31,30,31,31,30,31,30,31