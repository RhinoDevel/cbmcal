#RetroDevStudio.MetaData.BASIC:2049,BASIC V2,uppercase,10,10
1000 REM --- SUB.PARAMS. ---
1020 P0=0:P1=0:P2=0
1040 P0$="":P1$="":P2$=""
1060 REM --- SUB.RETURNS ---
1080 R0=0:R1=0:R2=0
1100 R0$="":R1$="":R2$=""
1120 REM --- OTHER GLOBAL VARS. --
1140 DIM DW(11):REM FOR WEEKDAY-BY-DATE
1160 DIM WD$(6):REM THE WEEKDAYS
1180 DIM MD(11):REM NON-LEAP MONTH DAYS
1200 I=0:J=0:REM COUNTERS
1220 B0=0:B1=0:B2=0:REM NUMBER BUFFERS
1240 IN$="":REM FOR USER INPUT
1260 CO$="":REM STORES A USER COMMAND
1280 YE$="":MO$="":DA$="":REM A DATE.
1300 YE=0:MO=0:DA=0:REM DATE AS INTS.
1320 WD=0:REM INDEX INTO WD$.
1340 REM --- INIT "CONSTANTS" ---
1360 FOR I=0 TO 11:READ DW(I):NEXT I
1380 FOR I=0 TO 6:READ WD$(I):NEXT I
1400 FOR I=0 TO 11:READ MD(I):NEXT I
1420 REM --- JUMP TO MAIN ---
1440 GOTO 2480
1460 REM --- SUBROUTINES ---
1461 REM *** LEAP YEAR YES/NO TO B2 ***
1462 IF YE/100<>INT(YE/100) GOTO 1467
1463 IF YE/400=INT(YE/400) GOTO 1478
1464 B2=0:RETURN:REM NOT A LEAP YEAR
1467 IF YE/4<>INT(YE/4) GOTO 1464
1478 B2=1:RETURN:REM IT IS A LEAP YEAR
1480 REM *** PRINT MONTH ***
1500 PRINT " ";
1520 FOR I=0 TO 5
1540 PRINT WD$(I);"  ";
1560 NEXT I
1580 PRINT WD$(6)
1600 PRINT " ";
1620 FOR I=0 TO 5
1640 IF I>=WD THEN 1680
1660 PRINT "  î¹  ";:GOTO 1700
1680 PRINT I-WD+1;" ";
1700 NEXT I
1720 PRINT I-WD+1;" "
1740 B0=I-WD+1+1
1760 B1=MD(MO-1)
1761 IF MO<>2 THEN 1780:REM NO FEBRUARY
1762 GOSUB1462:REM IS A LEAP YEAR?
1763 IF B2=0 THEN 1780:REM NO LEAP YEAR
1764 B1=B1+1:REM 29 DAYS IN FEBRUARY
1780 J=0
1800 FOR I=B0 TO B1
1820 IF I<10 THEN PRINT " ";
1840 PRINT I;
1860 J=J+1
1880 IF J<7 THEN 1940
1900 PRINT""
1920 J=0
1940 NEXT I
1960 PRINT""
1980 RETURN
2000 REM *** FILL WD BY YE,MO & DA ***
2020 B0=YE
2040 IF MO<3 THEN B0=B0-1
2060 WD=B0+INT(B0/4)
2080 WD=WD-INT(B0/100)
2100 WD=WD+INT(B0/400)
2120 WD=WD+DW(MO-1)+DA
2140 WD=WD-INT(WD/7)*7
2160 WD=WD+6:WD=WD-INT(WD/7)*7
2180 RETURN
2200 REM *** MONTH FROM INPUT CMD. ***
2220 YE$=MID$(IN$,2,4)
2240 MO$=MID$(IN$,6,2)
2260 REM *** MONTH FROM YE$ AND MO$ ***
2280 YE=VAL(YE$)
2300 MO=VAL(MO$)
2320 REM *** MONTH FROM YE AND MO ***
2340 DA=1
2360 GOSUB 2020:REM FILLS WD
2380 GOSUB 1500:REM PRINTS MONTH
2400 RETURN
2420 REM --------------------
2440 REM --- MAIN ROUTINE ---
2460 REM --------------------
2480 INPUT"";IN$:REM READS COMMANDLINE
2500 CO$=LEFT$(IN$,1):REM GETS COMMAND
2520 IF CO$<>"M" THEN 2560
2540 GOSUB 2220:GOTO 2480
2560 IF CO$<>"Q" THEN 2600
2580 END
2600 GOTO 2480
2620 END
2640 REM --- DATA ---
2660 REM FOR WEEKDAY-BY-DATE CALC.:
2680 DATA 0,3,2,5,0,3,5,1,4,6,2,4
2700 REM FOR WEEKDAY ITERATION:
2720 DATA MO,TU,WE,TH,FR,SA,SU
2740 REM DAYS OF MONTH (NON-LEAP YEAR)
2760 DATA 31,28,31,30,31,30,31,31,30,31,30,31