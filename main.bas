#RetroDevStudio.MetaData.BASIC:2049,BASIC V2,uppercase,10,10
1000 REM --- SUB.PARAMS. ---
1020 P0=0:P1=0:P2=0
1040 P0$="":P1$="":P2$=""
1060 REM --- SUB.RETURNS ---
1080 R0=0:R1=0:R2=0
1100 R0$="":R1$="":R2$=""
1120 REM --- OTHER GLOBAL VARS. --
1140 DIM DW(11):REM FOR WEEKDAY-BY-DATE
1160 DIM WD$(6):REM THE WEEKDAYS
1180 DIM MD(11):REM NON-LEAP MONTH DAYS
1200 I=0:J=0:REM COUNTERS
1220 B0=0:B1=0:REM NUMBER BUFFERS
1240 IN$="":REM FOR USER INPUT
1260 CO$="":REM STORES A USER COMMAND
1280 YE$="":MO$="":DA$="":REM A DATE.
1300 YE=0:MO=0:DA=0:REM DATE AS INTS.
1320 WD=0:REM INDEX INTO WD$.
1340 REM --- INIT "CONSTANTS" ---
1360 FOR I=0 TO 11:READ DW(I):NEXT I
1380 FOR I=0 TO 6:READ WD$(I):NEXT I
1400 FOR I=0 TO 11:READ MD(I):NEXT I
1420 REM --- JUMP TO MAIN ---
1440 GOTO 2400
1460 REM --- SUBROUTINES ---
1480 REM *** PRINT MONTH ***
1500 PRINT " ";
1510 FOR I=0 TO 5
1520 PRINT WD$(I);"  ";
1540 NEXT I
1560 PRINT WD$(6)
1570 PRINT " ";
1580 FOR I=0 TO 5
1600 IF I>=WD THEN 1640
1620 PRINT "  î¹  ";:NEXT I
1640 PRINT I-WD+1;" ";
1660 NEXT I
1680 PRINT I-WD+1;" "
1700 B0=I-WD+1+1
1720 REM TODO: ADD LEAP YEAR SUPPORT!
1740 J=0
1760 FOR I=B0 TO MD(MO-1)
1765 IF I<10 THEN PRINT " ";
1780 PRINT I;
1800 J=J+1
1820 IF J<7 THEN 1880
1840 PRINT""
1860 J=0
1880 NEXT I
1890 PRINT""
1900 RETURN
1920 REM *** FILL WD BY YE,MO & DA ***
1940 B0=YE
1960 IF MO<3 THEN B0=B0-1
1980 WD=B0+INT(B0/4)
2000 WD=WD-INT(B0/100)
2020 WD=WD+INT(B0/400)
2040 WD=WD+DW(MO-1)+DA
2060 WD=WD-INT(WD/7)*7
2080 WD=WD+6:WD=WD-INT(WD/7)*7
2100 RETURN
2120 REM *** MONTH FROM INPUT CMD. ***
2140 YE$=MID$(IN$,2,4)
2160 MO$=MID$(IN$,6,2)
2180 REM *** MONTH FROM YE$ AND MO$ ***
2200 YE=VAL(YE$)
2220 MO=VAL(MO$)
2240 REM *** MONTH FROM YE AND MO ***
2260 DA=1
2280 GOSUB 1940:REM FILLS WD
2300 GOSUB 1500:REM PRINTS MONTH
2320 RETURN
2340 REM --------------------
2360 REM --- MAIN ROUTINE ---
2380 REM --------------------
2400 INPUT"";IN$:REM READS COMMANDLINE
2420 CO$=LEFT$(IN$,1):REM GETS COMMAND
2440 IF CO$<>"M" THEN 2480
2460 GOSUB 2140:GOTO 2400
2480 IF CO$<>"Q" THEN 2520
2500 END
2520 GOTO 2400
2540 END
2560 REM --- DATA ---
2580 REM FOR WEEKDAY-BY-DATE CALC.:
2600 DATA 0,3,2,5,0,3,5,1,4,6,2,4
2620 REM FOR WEEKDAY ITERATION:
2640 DATA MO,TU,WE,TH,FR,SA,SU
2660 REM DAYS OF MONTH (NON-LEAP YEAR)
2680 DATA 31,28,31,30,31,30,31,31,30,31,30,31